#compdef td

_td() {
	local -a commands=(
		"add:add a new task"
		"do:mark tasks as done"
		"ls:list tasks"
		"modify:modify tasks"
		"restore:undo the last modification in todo.json/done.json using the bak file"
		"rm:delete tasks"
		"undo:undo tasks"
		"help:display help message"
  )
  describe_commands() {
    _describe "commands" commands
  }

  local command_name=${words[2]}
  local -a command_flags=(
    "(- *)"{-h,--help}"[show CLI help]"
    "(- *)"{-v,--version}"[show version]"
  )
  set_flags() {
    case $command_name in
      add)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
          "(-P --priority)"{-P,--priority}"[priority (A-Z)]: :compadd {A..Z}"
          "(-p --project)*"{-p,--project}"[one or more projects]:"
          "(-c --context)*"{-c,--context}"[one or more contexts]:"
          "(-d --due)"{-d,--due}"[due date]:"
        )
      ;;
      do)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
        )
      ;;
      undo)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
        )
      ;;
      rm)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
          "(-D --done)"{-D,--done}"[delete done tasks]"
        )
      ;;
      restore)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
          "(-f --force)"{-f,--force}"[force overwriting without confirmation]"
          "(-D --done)"{-D,--done}"[restore done.json]"
        )
      ;;
      ls)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
          "(-a --all)"{-a,--all}"[list all tasks including done ones]"
          "(-D --done)"{-D,--done}"[list only done tasks]"
          "(-P --priority)"{-P,--priority}"[priority (A-Z)]: :compadd {A..Z}"
          "(-p --project)*"{-p,--project}"[one or more projects]:"
          "(-c --context)*"{-c,--context}"[one or more contexts]:"
        )
      ;;
      modify)
        command_flags=(
          "(- *)"{-h,--help}"[show command help]"
          "(-D --done)"{-D,--done}"[modify done tasks]"
          "(-P, --priority)"{-P,--priority}"[priority (A-Z)]: :compadd {A..Z}"
          "(-p --project)*"{-p,--project}"[one or more projects (overwrite all)]:"
          "(-c --context)*"{-c,--context}"[one or more contexts (overwrite all)]:"
          "(-d --due)*"{-d,--due}"[modify due date]:"
          "--delete-priority[delete priority]"
          "--delete-projects[delete projects]"
          "--delete-contexts[delete contexts]"
          "--delete-due[delete due date]"
        )
      ;;
    esac
  }
  if [ $CURRENT -gt 2 ]; then
    set_flags
  fi

  _arguments "1: :describe_commands" $command_flags
}

_td
